/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.ORM.MySQL;

use Runtime.CoreStruct;
use Runtime.ORM.Cursor;
use Runtime.ORM.ProviderInterface;
use Runtime.ORM.MySQL.Driver;


struct Provider extends CoreStruct implements ProviderInterface
{
	
	string host = "";
	int port = 3306;
	string username = "";
	string password = "";
	string db = "";
	string prefix = "";
	bool log_query = false;
	
	
	/**
	 * Returns providers hash
	 */
	pure string getHash(Provider p) =>
		p.hostname ~ "|" ~
		p.port ~ "|" ~
		p.username ~ "|" ~
		p.password ~ "|" ~
		p.db ~ "|" ~
		p.prefix
	;
	
	
	
	/**
	 * Execute query
	 */
	static fn query(string sql, Dict arr) =>
		async Cursor (Provider p) use (sql, arr)
		{
			Driver d = @ -> method getDriver(classof Driver);
			Cursor cursor = await d.query(p, sql, arr);
			return cursor;
		}
	;
	
	
	
	/**
	 * Insert query
	 */
	static fn insert(string table_name, Dict arr) =>
		async void (Provider p) use (table_name, arr)
		{
			Vector keys = new Vector();
			Vector values = new Vector();
			
			arr.each
			(
				void (string value, string key) use (keys, values)
				{
					keys.push("`" ~ key ~ "`");
					values.push(":" ~ key);
				}
			);
			
			string sql = "insert into " ~ table_name ~
				" (" ~ rs::join(", ", keys) ~ ") values (" ~ rs::join(", ", values) ~ ")"
			;
			
			Cursor cursor = p -> method query(sql, arr);
			return cursor;
		}
	;
	
	
	
	/**
	 * Update query
	 */
	static fn update(string table_name, Dict where, Dict arr) =>
		async void (Provider p) use (table_name, where, arr)
		{
			Map items = new Map();
			Vector v_set = new Vector();
			Vector v_where = new Vector();
			
			where.each
			(
				void (string value, string key) use (items, v_set, v_where)
				{
					v_where.push("`" ~ key ~ "` = :" ~ key);
					items.set(key, value);
				}
			);
			
			arr.each
			(
				void (string value, string key) use (items, v_set, v_where)
				{
					v_set.push("`" ~ key ~ "` = :" ~ key);
					items.set(key, value);
				}
			);
			
			string sql = "update " ~ table_name ~ " set " ~ rs::join(", ", v_set) ~
				" where " ~ rs::join(" and ", v_where)
			;
			
			Cursor cursor = p -> method query(sql, items);
			return cursor;
		}
	;
	
}